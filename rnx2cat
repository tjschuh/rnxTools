#!/bin/tcsh -f
#
# Take 2 RINEX files and merge them
# into 1 new concatenated RINEX file
#
# INPUT:
#
# $1       first RINEX file
# $2       second RINEX file
#
# OUTPUT:
#
# cat.rnx    new concatenated RINEX file
#
# Originally written by tschuh-at-princeton.edu, 07/19/2021
# Last modified by tschuh-at-princeton.edu, 08/09/2021

set warnin = 0
if($#argv < 2)then
  echo ' '
  echo 'RNX2CAT: At least two input arguments expected.'
  echo ' '
  echo '---------> RNX2CAT file1 file2'
  echo '---------> where file1 and file2 are consecutive RINEX files'
  echo ' '
  @ warnin +=1
  goto label999
endif

# Input parsing
set infile1 = $1
set infile2 = $2

# Output parsing
set outfile = `echo cat.rnx`

#####################################################################
# now the code starts

# copy all of $infile1 to cat.rnx
cat $infile1 >! $outfile

# find the number of header lines of $infile2
set nrh = `rnx2hdr $infile2`

# delete header lines of $infile2 and append rest to cat.rnx
sed '1,'"$nrh"'d' $infile2 >> $outfile

# save the end time from $infile2
set lyr = `awk '/TIME OF LAST OBS/ {print $1}' $infile2`
set lmo = `awk '/TIME OF LAST OBS/ {print $2}' $infile2`
set ldy = `awk '/TIME OF LAST OBS/ {print $3}' $infile2`
set lhr = `awk '/TIME OF LAST OBS/ {print $4}' $infile2`
set lmn = `awk '/TIME OF LAST OBS/ {print $5}' $infile2`
set lsc = `awk '/TIME OF LAST OBS/ {print $6}' $infile2`

# call rnx2tme to correct the end time of $outfile
# so that it has the end time from $infile2
rnx2tme $outfile $lyr $lmo $ldy $lhr $lmn $lsc

#####################################################################
label999:
echo ' '
if ($warnin == 1) then
    echo "Script RNX2CAT stopped, $warnin warning message generated"
else
    echo "Script RNX2CAT finished, $warnin warning messages generated"
endif
echo ' '
