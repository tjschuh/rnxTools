#!/bin/tcsh
#
# given a path/to/a/RINEX/directory output a station list also with approx positions
#
# INPUT:
#
# $1       directory containing RINEX files of interest
#
# OUTPUT:
#
# file within $1 called station.list with 4 columns: marker/station name, approx position x, y, z
#
# Originally written by tschuh-at-princeton.edu, 07/06/2022
# Last modified by tschuh-at-princeton.edu, 07/29/2022

set warnin = 0
if($#argv != 1)then
  echo ' '
  echo 'RNXSTATLIST: Only one input argument expected:'
  echo ' '
  echo '----------> RNXSTATLIST rinexdir'
  echo '----------> where rinexdir is path to directory containing RINEX files'
  echo ' '
  @ warnin +=1
  goto label999
endif

################################################################################

# redefine input for convenience
set rinexdir = $1

# list all RINEX files within $rinexdir
set files = `find $rinexdir -type d -name "???" -exec find "{}" -type f -name "*.???" \;`

# awk marker name and approx position for each RINEX file
# and output them to file called stationlist
foreach file ($files[*])
    set mkr = `awk '/MARKER NAME/ {print $1}' $file`
    set pos = `awk '/APPROX POS/ {print $1,$2,$3}' $file`
    echo $mkr $pos >>! $rinexdir/stations.list-draft
end

# before the sorting of unique lines, remove both empty columns and ones with blank approx positions 
awk 'NF>1' $rinexdir/stations.list-draft > $rinexdir/stations.list-draft2

# save only unique lines based on column 1 through column 1
sort -u -k 1,1 $rinexdir/stations.list-draft2 > $rinexdir/stations.list

rm -f $rinexdir/stations.list-draft*

################################################################################

label999:
echo ' '
if ($warnin == 1) then
    echo "Script RNXSTATLIST stopped, $warnin warning message generated"
else
    echo "Script RNXSTATLIST finished, $warnin warning messages generated"
endif
echo ' '
