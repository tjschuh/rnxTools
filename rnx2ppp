#!/bin/tcsh
#
# run GipsyX on a RINEX file that has
# been split into 2 30 min pieces and
# write solutions to a new file along
# with timestamps and approx position
#
# INPUT:
#
# $1         RINEX file
#
# OUTPUT:
#
# data.ppp    multi-column data file with XYZ positions before and after GipsyX
#
# REQUIRES:
#
# A full installation of GipsyX, in particular, GD2E 
#
# Originally written by tschuh-at-princeton.edu, 07/07/2021
# Last modified by tschuh-at-princeton.edu, 07/09/2021

# currently doesnt tell the difference between
# prepos and postpos (done in post-script in MATLAB)

set files = $1

mkdir temp

unalias cd

foreach file ($files[*])
	set nrh = `rnx2hdr $file`
        set mark = `head -$nrh $file | awk '/MARKER NAME/ {print $1}'`

        cd temp
	 
	# subdivide
        set offsets   = (    0  1800 )
        set durations = ( 1800  1800 )

	set counter = 0
	foreach offset ($offsets[*])
	    @ counter++
            /usr/bin/cp ../$file .
	    rnx2rnx $file $offset $durations[$counter]
 	    gd2e.py -rnxFile $file:r.rnx
            set prepos = `rnx2pos $file:r.rnx`
            set epc = `rnx2epc $file:r.rnx`
 	    set postpos = `awk '/'"$mark"'/ {print $2,$3,$4}' Summary`
            echo $prepos $epc $postpos $offset $durations[$counter] >>! ../data.ppp
	    rm -rf *
         end 
    cd ..
end

rmdir temp
