#!/bin/tcsh
#
# automatically process RINEX files from before 2020 using PRIDE PPP-AR
# only requires 1 input, a few pre-requsites (see below), and for the
# user and all files to be in the same directory
#
# INPUT:
#
# $1    the file extension of your RINEX files i.e. what follows .
#
# OUTPUT:
#
# sorted directories with processed data in each
#
# PREREQUISITES:
#
# all RINEX .obs files must be in standard .o format (use RNXCMP tools)
# all RINEX .obs files must have "TIME OF FIRST OBS" and "TIME OF LAST OBS" (use rnx2tlo)
# copy Config_template_* to working directory with appropriate edits corresponding to the day of year
# copy and "gzip -d" appropriate RINEX .nav file into working directory
# data parent directory structure must be /blah/MarkerName/DayOfYear/
#
# Originally written by tschuh-at-princeton.edu, 04/25/2022

set warnin = 0
if($#argv != 1)then
  echo ' '
  echo 'RAJ2PRD: One input argument expected:'
  echo ' '
  echo '----------> RAJ2PRD extension'
  echo '----------> where extension is RINEX file extension'
  echo ' '
  @ warnin +=1
  goto label999
endif

# redefine inputs for convenience
set ext = $1
#set int = $2

unalias cd

# put all .obs files into appropriate directories called "file####"
rnx2dir $ext

# count number of directories named file*
set max = `find file* -maxdepth 0 -type d -print | wc -l`
set range = `seq 1 $max`

# need to copy nav file from working directory to all sub-directories

foreach num ($range[*])
    # cd into file####
    set name = `printf "file%3.3i" $num`
    cd $name

    # copy and edit config file for right rinex path
    cp ../config_template_* .
    set oldpath = `awk '/Rinex directory/ {print $4}' config_template_*`
    set newpath = `pwd`
    sed -i 's@'"$oldpath"'@'"$newpath"'@' config_template_*

    # get marker name from 2 directories up
    # get DOY from 1 directory up
    set mkr = `echo $PWD | awk -F "/" '{print $(NF-2)}'`
    set doy = `echo $PWD | awk -F "/" '{print $(NF-1)}'`

    # build date from RINEX file using awk
    set fYYYY = `awk '/TIME OF FIRST OBS/ {print $1}' *.{$ext}`
    set fYYYY = `printf "%4.4i" $fYYYY`
    set fMM = `awk '/TIME OF FIRST OBS/ {print $2}' *.{$ext}`
    set fMM = `printf "%02.0f" $fMM`
    set fDD = `awk '/TIME OF FIRST OBS/ {print $3}' *.{$ext}`
    set fDD = `printf "%02.0f" $fDD`
    set fdate = `printf $fYYYY$fMM$fDD`

    set lYYYY = `awk '/TIME OF LAST OBS/ {print $1}' *.{$ext}`
    set lYYYY = `printf "%4.4i" $lYYYY`
    set lMM = `awk '/TIME OF LAST OBS/ {print $2}' *.{$ext}`
    set lMM = `printf "%02.0f" $lMM`
    set lDD = `awk '/TIME OF LAST OBS/ {print $3}' *.{$ext}`
    set lDD = `printf "%02.0f" $lDD`
    set ldate = `printf $lYYYY$lMM$lDD`

    # extract last 2 digits of year from $fYYYY
    set YY = `echo $fYYYY | cut -c 3,4`

    # strip file extension from RINEX .obs file
    set outfile = `/bin/ls -1 *.{$ext} | xargs -n1 basename`

    # change name of RINEX .obs file appropriately
    mv *.{$ext} {$mkr}{$doy}0.{$YY}o

    # copy RINEX .nav file to working directory and change name
    cp ../{$mkr}{$doy}0.{$YY}n .
    mv {$mkr}{$doy}0.{$YY}n brdm{$doy}0.{$YY}p
    
    # run PRIDE (hard-coded sampling rate at 1)
    pdp3 config_* $fdate $ldate $mkr k 1 n >! $outfile:r.cfg

    # run kin2prd to get .prd and .hdr files
    kin2prd $YYYY/$doy/kin_*

    # go back to parent directory and prepare to run again
    cd ..
end

################################################################################
label999:
echo ' '
if ($warnin == 1) then
    echo "Script RAJ2PRD stopped, $warnin warning message generated"
else
    echo "Script RAJ2PRD finished, $warnin warning messages generated"
endif
echo ' '
