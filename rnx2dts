#!/bin/tcsh
#
# Takes a RINEX file and determine if it's missing any seconds of
# measurements....  
#
# INPUT:
#
# $1     a RINEX file name
#
# OUTPUT:
#
# To screen.  
#
# REQUIRES:
#
# A full installation of GIPSY, in particular, DATE2SEC
#
# Last modified by tschuh-at-princeton.edu on 06/11/2021

set hlength = `./rnx2hdr $1`

# text match all the dates in the RINEX file

# convert dates to seconds using GipsyX command DATE2SEC

# go through every line and subtract the value on the previous line

set dum = `awk '/[A-z]/ && NR>'"$hlength"' && $1!~/[A-z]/ {printf "20%2.2i-%2.2i-%2.2i %2.2i:%2.2i:%2.2i\n",$1,$2,$3,$4,$5,$6}' $1 | date2sec | awk 'NR > 1 { print $0 - prev } { prev = $0 }'`

# tests whether any one is identical to all of them
set thedif = $dum[1]

# if the difference between lines is something
# other than 1, then report this (make variable err)
foreach num ($dum[*])
    if ($num != $thedif) then
       set err = "0"
    endif
end

# test the existence of the variable err
# if it exists, then kill the next program
# otherwise continue
if ( $?err ) then
    echo "TEST FAILED: Not all increments are the same"
else
    echo "TEST SUCCESSFUL: All increments are $thedif s"
endif

