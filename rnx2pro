#!/bin/tcsh
#
# automatially prepare and process RINEX files from
# various stations across a range of days
#
# INPUT:
#
# $1        ASCII file containing list of 4-character station/marker codes i.e. 0842
# $2        year data is from (currently doesnt work with multiple years)
# $3        start day of year (doy) in range of days being processed
# $4        end day of year (doy) in range of days being processed
# $5        sampling interval of RINEX files in seconds (currently cant take multiple values)
# $6        "yes" or "no" switch for ambiguity resolution (AR) when using PRIDE PPP-AR
# $7        "yes" or "no" switch for tidal corrections when using PRIDE PPP-AR
# $8        full file path to PRIDE PPP-AR configuration file used throughout run
# $9        path/to/directory/containing/directories/of/RINEX/observation/files
# $10       path/to/directory/containing/directories/of/RINEX/navigation/files (if different)
#
# AUXILLARY CODE NEEDED:
#
# rnxTools
#
# EXAMPLE:
#
# rnx2pro path/2/stations.list 2011 67 67 1 no no path/2/config_daily_GEONET_JAPAN_2011_DAYS_67_TO_85 /home/seismogeodesy/DATA/NETWORKS/GEONET_JAPAN/rinex1hz/ /home/seismogeodesy/DATA/NETWORKS/GEONET_JAPAN/rinex/
#
# Originally written by tschuh-at-princeton.edu, 06/07/2022
# Last modified by tschuh-at-princeton.edu, 07/28/2022

# start in directory with which you want station directories
# containing various runs i.e. 0842/ 0843/ 0844/

# Notes:
# hour long files are being linked and then concatenated to make file day long ofile
# but that in itself is not linked and about 90 MB in size so we will delete it at the end
# need to eventually build in concatenating 15 minute files

# To do:
# consider what to do with .O files

set warnin = 0
if($#argv == 9)then
set rinexdir = $9
set navdir = $9
else if($#argv == 10)then
set rinexdir = $9
set navdir = $10
else
  echo ' '
  echo 'RNX2PRO: ten input arguments expected:'
  echo ' '
  echo '----------> RNX2PRO statlist yr sdoy edoy sps ar tides prideconfig rinexdir [navdir]'
  echo '----------> where statlist is a list of 4-character station/marker codes'
  echo '----------> where yr is year'
  echo '----------> where sdoy is start day of year'
  echo '----------> where edoy is end day of year'
  echo '----------> where sps is sampling rate in seconds'
  echo '----------> where ar is either yes for ambiguity resolution or no for no AR'
  echo '----------> where tides is either yes for corrections or no for no corrections'
  echo '----------> where prideconfig is full file path to PRIDE PPP-AR configuration file'
  echo '----------> where rinexdir is directory containing day/station full RINEX files'
  echo '----------> where navdir is directory containing day/station navigation files'
  echo ' '
  @ warnin +=1
  goto label999
endif

#########################################################################

# define variables from inputs
set stlist = $1
set yr = $2
set split = ($yr:as/20/ /)
set yr2 = $split[1]
set sdoy = `printf "%03.0f" $3`
set edoy = `printf "%03.0f" $4`
set sps = $5
set ar = $6
set tides = $7
set prideconfig = $8

# define sequence of days
set days = `seq -f %03g $sdoy $edoy`

# use the natural version of ls and cd in case it's aliased
unalias ls
unalias cd

# loop over marker names
foreach mkr (`awk '{print $1}' $stlist`)
    mkdir $mkr
    cd $mkr
    echo 'Working on marker : '$mkr
    foreach day ($days[*])
	# if 24 hour file already exists, we can skip concatenation part
	if (-f $rinexdir/$yr/$day/{$mkr}{$day}0.{$yr2}o) then
	    # link this ofile and nfile and change name of nfile
	    set ofile = `ls $rinexdir/$yr/$day/{$mkr}{$day}0.{$yr2}o`
	    ln -s $ofile
	    set nfile = `ls $navdir/$yr/$day/{$mkr}*.{$yr2}{n,N}`
	    ln -s $nfile
	    mv -f {$mkr}*.{$yr2}n brdm{$day}0.{$yr2}p
	else
	    # get standard RINEX ofiles for all hours of DoY and link them
	    ls $rinexdir/$yr/$day/?/{$mkr}*.{$yr2}o >! templist       	
	    foreach ofile ( `cat templist` )
		ln -s $ofile
		rnx2tlo $ofile
	    end
	    rm -f templist

	    # get corresponding nav file, link it, and change its name
	    set nfile = `ls $navdir/$yr/$day/{$mkr}*.{$yr2}n`
	    ln -s $nfile
	    # change name of nav file so PRIDE acknowledges it
	    # navfile --> brdm{$day}0.{$yr2}p
	    mv -f {$mkr}*.{$yr2}n brdm{$day}0.{$yr2}p

	echo 'CONCATENATION PART'
	    # now concatenate all the RINEX files for each day into 1 file
	    set files = `/usr/bin/ls {$mkr}{$day}[a-b,A-B].{$yr2}o`
	    echo $files
	    rnx2cat $files ; mv -f cat.rnx tempfile
	    set files = `/usr/bin/ls {$mkr}{$day}[c-x,C-X].{$yr2}o`
	    foreach file ($files[*])
		echo $file
		rnx2cat tempfile $file ; mv -f cat.rnx tempfile
	    end
	    rm -rf {$mkr}{$day}[a-x,A-X].{$yr2}o
	    mv -f tempfile {$mkr}{$day}0.{$yr2}o
	endif
    end
    echo '################################################'
    echo ' '
    echo 'PRIDE PART'
    # actually run PRIDE (with various options depending on user input)
    set pth = `pwd`
    if ($ar == yes) then
	if ($tides == yes) then
	    pdp3 -cfg $prideconfig -m K -s $yr/$sdoy -e $yr/$edoy -n $mkr -i $sps $pth/$mkr/{$mkr}{$sdoy}0.{$yr2}o
	else if ($tides == no) then
	    pdp3 -cfg $prideconfig -m K -s $yr/$sdoy -e $yr/$edoy -n $mkr -i $sps -toff SOP $pth/$mkr/{$mkr}{$sdoy}0.{$yr2}o
	else
	    echo ' '
	    echo 'Please select either yes or no for tidal corrections'
	    echo ' '
	    @ warnin +=1
	    goto label999
	endif
    else if ($ar == no) then
	if ($tides == yes) then
	    pdp3 -cfg $prideconfig -m K -s $yr/$sdoy -e $yr/$edoy -n $mkr -i $sps -f $pth/$mkr/{$mkr}{$sdoy}0.{$yr2}o
	else if ($tides == no) then
	    pdp3 -cfg $prideconfig -m K -s $yr/$sdoy -e $yr/$edoy -n $mkr -i $sps -toff SOP -f $pth/$mkr/{$mkr}{$sdoy}0.{$yr2}o
	else
	    echo ' '
	    echo 'Please select either yes or no for tidal corrections'
	    echo ' '
	    @ warnin +=1
	    goto label999
	endif
    else
	echo ' '
	echo 'Please select either yes or no for ambiguity resolution (AR)'
	echo ' '
	@ warnin +=1
	goto label999
    endif

    # PRIDE run is over so can delete nav file and ofile(s)
    rm -f {$mkr}*0.{$yr2}{o} brdm{$day}0.{$yr2}p

    # cd into output directory i.e. 2011/067-084/ and run code from kin2prd
    if ("$sdoy" == "$edoy") then
	cd $yr/$sdoy/
    else
	cd $yr/$sdoy-$edoy/
    endif

    set infile = `ls kin_*`
    set hdrend = `rnx2hdr $infile`
    set hdrend = `echo "$hdrend+1" | bc`
    # create hdr and prd files from original file
    awk 'NR<='"$hdrend"' {print}' $infile >! $infile:r.hdr
    awk 'NR>'"$hdrend"' {print}' $infile >! $infile:r.prd
    # remove any stray asterisks (*) in the prd file
    if ($OSTYPE == 'linux') then
	sed -i 's/\*/ /g' $infile:r.prd
    else if ($OSTYPE == 'darwin') then
	# on MacOS need to add '' in beginning for creation of output file
	sed -i '' 's/\*/ /g' $infile:r.prd
    else
	echo ' '
	echo 'Code did not run'
	echo ' '
	@ warnin +=1
	goto label999
    endif

    # need to extract approx position xyz --> it's inside stlist columns 2-4
    # in the wrong directory
    set x = `awk '{print $2}' $stlist`
    set y = `awk '{print $3}' $stlist`
    set z = `awk '{print $4}' $stlist`

    # run prd2msd.py here to generate mseed file
    prd2msd.py -s $sps -x $x $y $z $infile:r.prd

    # copy mseed file(s) and .hdr file back up to main directory
    cp GN* ../../
    cp *.hdr ../../
    # cd back up to run directory and delete everything except .hdr and .mseed files
    cd ../../
    rm -rf $yr/
    
    # cd back up to base directory with all stations
    cd ../
end

##############################################################################

label999:
echo ' '
if ($warnin == 1) then
    echo "Script RNX2PRO stopped, $warnin warning message generated"
else
    echo "Script RNX2PRO finished, $warnin warning messages generated"
endif
echo ' '
